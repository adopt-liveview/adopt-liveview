%{
title: "Editando um produto",
author: "Lubien",
tags: ~w(getting-started),
section: "CRUD",
description: "Formul√°rios por√©m para editar algo existente",
previous_page_id: "deleting-data",
}

---

Para finalizarmos o CRUD iremos criar um formul√°rio de edi√ß√£o de produto. Vamos ver como este pode ser extremamente parecido com o formul√°rio de cria√ß√£o de produto.

%{
title: "Esta aula √© uma continua√ß√£o direta da aula anterior",
type: :warning,
description: ~H"""
Se voc√™ entrou direto nesta aula talvez seja confuso pois ela √© uma continua√ß√£o direta do c√≥digo da aula anterior. Caso voc√™ queira pular a aula anterior e come√ßar direto nesta voc√™ pode clonar a vers√£o inicial para esta aula usando o comando <code>`git clone https://github.com/adopt-liveview/first-crud.git --branch deleting-data-done`</code>.
"""
} %% .callout

## De volta ao Context

Vamos voltar ao nosso `lib/super_store/catalog.ex` e adicionar uma nova fun√ß√£o:

```elixir
defmodule SuperStore.Catalog do
  # ...

  def update_product(%Product{} = product, attrs) do
    product
    |> Product.changeset(attrs)
    |> Repo.update()
  end
end
```

Diferente de `create_product/1` que apenas recebe os atributos, para atualizarmos um produto precisamos do dado original para poder aplicar as altera√ß√µes. Nossa fun√ß√£o `Catalog.update_product/2` recebe o struct original e as modifica√ß√µes, aplica o changeset e, usando a fun√ß√£o [`Repo.update/2`](https://hexdocs.pm/ecto/Ecto.Repo.html#c:update/2) retorna `{:ok, %Product{}}` ou `{:error, %Ecto.Changeset{}}`.

### Testando no `iex`

Usando o Elixir Interativo podemos pegar o √∫ltimo produto com `product = SuperStore.Catalog.list_products() |> List.last` e o atualiz√°-lo usando `SuperStore.Catalog.update_product(product, %{name: "Edited"})`:

```elixir
$ iex -S mix

[info] Migrations already up
Erlang/OTP 26 [erts-14.2.2] [source] [64-bit] [smp:10:10] [ds:10:10:10] [async-threads:1] [jit]

Interactive Elixir (1.16.1) - press Ctrl+C to exit (type h() ENTER for help)

iex(1)> product = SuperStore.Catalog.list_products() |> List.last

[debug] QUERY OK source="products" db=0.0ms idle=823.0ms
SELECT p0."id", p0."name", p0."description" FROM "products" AS p0 []
‚Ü≥ :elixir.eval_external_handler/3, at: src/elixir.erl:405

%SuperStore.Catalog.Product{
  __meta__: #Ecto.Schema.Metadata<:loaded, "products">,
  id: 7,
  name: "asda",
  description: "asd"
}

iex(2)> SuperStore.Catalog.update_product(product, %{name: "Edited"})

[debug] QUERY OK source="products" db=0.7ms idle=539.3ms
UPDATE "products" SET "name" = ? WHERE "id" = ? ["Edited", 7]
‚Ü≥ :elixir.eval_external_handler/3, at: src/elixir.erl:405

{:ok,
 %SuperStore.Catalog.Product{
   __meta__: #Ecto.Schema.Metadata<:loaded, "products">,
   id: 7,
   name: "Edited",
   description: "asd"
 }}
iex(3)>
```

Note que nos segundo argumento passamos apenas o nome. Nosso changeset requer uma `description` obrigatoriamente por√©m, como o produto original j√° possui uma descri√ß√£o, essa valida√ß√£o passa.

## Construindo nossa LiveView

Vamos escrever o c√≥digo da LiveView passo-a-passo de modo que possamos ver as semelhan√ßas com a ProductLive.Create. Na pasta `lib/super_store_web/live/product_live/` crie um arquivo `edit.ex`.

### Come√ßando

```elixir
defmodule SuperStoreWeb.ProductLive.Edit do
  use SuperStoreWeb, :live_view
  alias SuperStore.Catalog
  alias SuperStore.Catalog.Product
end
```

O primeiro passo √© criar o m√≥dulo e `use SuperStoreWeb, :live_view`. Em seguida, adicionamos dois alias √∫teis para o que vem a seguir.

```elixir
def mount(%{"id" => id}, _session, socket) do
  product = Catalog.get_product!(id)

  form =
    Product.changeset(product)
    |> to_form()

  {:ok, assign(socket, form: form, product: product)}
end
```

### O `mount/3`

Em nossa fun√ß√£o `mount/3` n√≥s recebemos como par√¢metro o `id` do produto. Logo mais iremos definir isso no router como `live "/products/:id/edit", ProductLive.Edit, :edit` portanto podemos garantir que haver√° este `id`.

O pr√≥ximo passo √© usar `product = Catalog.get_product!(id)` para recuperar o produto pelo `id`. Vale lembrar que se n√£o houver um produto com este `id` um erro 404 ser√° automaticamente gerado como vimos em aulas anteriores.

Definimos nosso `form` como um changeset que recebe o produto original. No formul√°rio de cria√ß√£o n√≥s usamos `Product.changeset(%Product{})`, ou seja, o produto vazio pois naquele momento n√£o existe um produto. Como estamos trabalhando com edi√ß√£o, todos os nossos changesets ir√£o receber o produto sendo editado.

Note tamb√©m que no assign passamos o `product`. Iremos usar esse assign n√£o s√≥ no nosso HEEx como tamb√©m em outros eventos.

### O evento de valida√ß√£o

```elixir
def handle_event("validate_product", %{"product" => product_params}, socket) do
  form =
    Product.changeset(socket.assigns.product, product_params)
    |> Map.put(:action, :validate)
    |> to_form()

  {:noreply, assign(socket, form: form)}
end
```

O evento de valida√ß√£o √© uma c√≥pia cuspida do formul√°rio de cria√ß√£o exceto que o `Product.changeset/2` recebe no primeiro argumento, ao inv√©s de `%Product{}` (o produto vazio), o `socket.assigns.product` que cont√©m o valor do produto sendo editado.

### O evento de salvar

```elixir
def handle_event("save_product", %{"product" => product_params}, socket) do
  socket =
    case Catalog.update_product(socket.assigns.product, product_params) do
      {:ok, %Product{} = product} ->
        put_flash(socket, :info, "Product ID #{product.id} updated!")

      {:error, %Ecto.Changeset{} = changeset} ->
        form = to_form(changeset)

        socket
        |> assign(form: form)
        |> put_flash(:error, "Invalid data!")
    end

  {:noreply, socket}
end
```

Mais uma vez o nosso evento √© uma c√≥pia cuspida do evento de criar produto. Renomeamos o evento para `"save_product"` para fazer sentido com o formul√°rio e trocamos a fun√ß√£o principal no `case` para `Catalog.update_product/2` passando mais uma vez o `socket.assign.product`. Mofificamos tamb√©m o `put_flash/2` para uma mensagem que faz mais sentido.

### A `render/1`

```elixir
def render(assigns) do
  ~H"""
  <.header>
    Editing Product <%= @product.id %>
    <:subtitle>Use this form to edit product records in your database.</:subtitle>
  </.header>

  <div class="bg-grey-100">
    <.form
      for={@form}
      phx-change="validate_product"
      phx-submit="save_product"
      class="flex flex-col max-w-96 mx-auto bg-gray-100 p-24"
    >
      <h1>Editing a product</h1>
      <.input field={@form[:name]} placeholder="Name" />
      <.input field={@form[:description]} placeholder="Description" />

      <.button type="submit">Send</.button>
    </.form>
  </div>

  <.back navigate={~p"/products/#{@product}"}>Back to product</.back>
  """
end
```

Nesta parte modificamos apenas os textos e o nome do evento do binding `phx-submit`. N√£o houve nenhuma modifica√ß√£o funcional exceto que o link do componente `<.back>` agora retorna pra p√°gina de visualizar o produto.

### Atualizando o router

Abra seu arquivo de router e adicione a rota `live "/products/:id/edit", ProductLive.Edit, :edit`. No momento seu router deve estar do seguinte modo:

```elixir
# ...
scope "/", SuperStoreWeb do
  pipe_through :browser

  live "/", ProductLive.Index, :index
  live "/products/new", ProductLive.New, :new
  live "/products/:id", ProductLive.Show, :show
  live "/products/:id/edit", ProductLive.Edit, :edit
end
# ...
```

## Adicionando um link pro formul√°rio

Temos uma p√°gina, mas nossos usu√°rios n√£o conhecem ela. Abra sua `ProductLive.Edit` e atualize apenas o componente `<.header>` para adicionar este `<:actions>`:

```elixir
<.header>
  Product <%= @product.id %>
  <:subtitle>This is a product record from your database.</:subtitle>
  <:actions>
    <.link patch={~p"/products/#{@product}/edit"}>
      <.button>Edit event</.button>
    </.link>
  </:actions>
</.header>
```

## C√≥digo final

Finalizado! Nossa aplica√ß√£o possui um CRUD completo. Ainda existem algumas coisas que podem melhorar e iremos ver isso em outra se√ß√£o mas se voc√™ seguiu o curso at√© ent√£o voc√™ j√° tem conhecimento suficiente para se virar criando seu pr√≥ximo CRUD!

Se voc√™ sentiu dificuldade de acompanhar o c√≥digo nesta aula voc√™ pode ver o c√≥digo pronto desta aula usando `git checkout editing-data-done` ou clonando em outra pasta usando `git clone https://github.com/adopt-liveview/first-crud.git --branch editing-data-done`.

## Resumindo!

- Usando `Repo.update/2` conseguimos atualizar um produto passando um changeset.
- Uma LiveView de formul√°rio de editar pode ser extremamente parecida com uma de criar um dado.
- Voc√™ j√° sabe fazer um CRUD completo em LiveView üòâ.
