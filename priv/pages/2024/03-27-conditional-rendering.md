%{
title: "Renderiza√ß√£o condicional",
author: "Lubien",
tags: ~w(getting-started),
section: "Estruturas de Controle",
description: "Renderizar ou n√£o renderizar, eis a quest√£o"
}

---

Vamos aprender algumas formas de renderizar HTML dependendo de certas condi√ß√µes. Crie e execute um arquivo chamado `toggle.exs`:

## Usando `if-else` para casos simples

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, show_information?: false)
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      <%= if @show_information? do %>
        <p>You're an amazing person!</p>
      <% else %>
        <p>You can't see this message!</p>
      <% end %>
    </div>

    <input type="button" value="Toggle" phx-click="toggle" >
    """
  end

  def handle_event("toggle", _params, socket) do
    socket = assign(socket, show_information?: !socket.assigns.show_information?)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

Vamos destrinchar este c√≥digo. O √∫nico assign que temos aqui se chama `show_information?` com valor inicial de falso. O evento `"toggle"` enviado pelo input simplesmente reverte o valor entre `true` e `false`. O que realmente √© novo aqui √© nosso bloco de `if-else`.

%{
title: "Interroga√ß√£o no meio do c√≥digo? Pode isso, Arnaldo?",
description: ~H"""
Em Elixir a interroga√ß√£o √© v√°lida em √°tomos e vari√°veis quando adicionada no final. Isso √© bem √∫til para denotar booleanos. Vai dizer que <code>`if @show_information?`</code> n√£o fica elegante?
"""
} %% .callout

Dentro de uma LiveView voc√™ pode fazer um `if-else` da seguinte maneira:

- Adicione um `<%= if condition do %>`. √â importante voc√™ usar a tag que cont√©m `=` sen√£o o HEEx vai entender que isso n√£o deve ser renderizado!
- Escreva qualquer HTML que estar√° no caso que deve ser renderizado.
- Adicione um `<% else %>`. Note que n√£o h√° um `=` desta vez. Se voc√™ adicionar ele o c√≥digo continua a funcionar por√©m um warning lhe avisar√° para remov√™-lo.
- Escreva qualquer HTML para o caso `else`.
- Adicione um `<% ende %>`. Mais uma vez, sem `=`.

Se voc√™ n√£o desejar mostrar um caso de `else` existem dua maneiras de fazer isso. A primeira √© simples: apenas remova o `<% else %>` e o conte√∫do dele! Crie e execute um arquivo chamado `toggle_without_else.ex`:

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, show_information?: false)
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      <%= if @show_information? do %>
        <p>You're an amazing person!</p>
      <% end %>
    </div>

    <input type="button" value="Toggle" phx-click="toggle" >
    """
  end

  def handle_event("toggle", _params, socket) do
    socket = assign(socket, show_information?: !socket.assigns.show_information?)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

## O atributo especial `:if`

Para casos em que voc√™ s√≥ possui o `if` o HEEx possui um atributo especial chamado `:if` em que voc√™ pode colocar diretamente na tag HTML. Crie e execute um arquivo chamado `toggle_special_if.exs`:

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, show_information?: false)
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      <p :if={@show_information?}>You're an amazing person!</p>
    </div>

    <input type="button" value="Toggle" phx-click="toggle" >
    """
  end

  def handle_event("toggle", _params, socket) do
    socket = assign(socket, show_information?: !socket.assigns.show_information?)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

No momento n√£o existe um attributo especial para `else` ent√£o como recomenda√ß√£o se voc√™ precisa apenas de `if` √© recomendado usar `:if` se voc√™ puder colocar em uma tag pai das coisas que entram na condi√ß√£o, caso contr√°rio utilize o primeiro exemplo com `if-else` demonstado aqui.

## Usando `case` para casos complexos

√â s√≥ uma quest√£o de tempo at√© voc√™ chegar em uma situa√ß√£o em que existe mais de duas possibilidades de renderizar algo. Elixir n√£o possui suporte para `else if` e com motivo: a prefer√™ncia √© `case` que √© muito mais poderoso!

Vamos criar um sistema simples de abas em LiveView. Crie e execute um arquivo chamado `case.exs`:

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, tab: "home")
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      <%= case @tab do %>
        <% "home" -> %>
          <p>You're on my personal page!</p>
        <% "about" -> %>
          <p>Hi, I'm a LiveView developer!</p>
        <% "contact" -> %>
          <p>Mail me to bot [at] company [dot] com</p>
      <% end %>
    </div>

    <input disabled={@tab == "home"} type="button" value="Open Home" phx-click="show_home" />
    <input disabled={@tab == "about"} type="button" value="Open About" phx-click="show_about" />
    <input disabled={@tab == "contact"} type="button" value="Open Contact" phx-click="show_contact" />
    """
  end

  def handle_event("show_" <> tab, _params, socket) do
    socket = assign(socket, tab: tab)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

Desta vez nosso assign virou `tab` que pode ser uma string entre `"home"`, `"about"` ou `"contact"`. Cada input cont√©m um `phx-click="show_NOME_DA_ABA"` de modo que nosso `handle_event/3` ir√° usar pattern matching em Elixir para aceitar qualquer evento que comece com `show_` e salva o restante do nome do evento em uma vari√°vel. Outro ponto simples por√©m interessante do nosso c√≥digo √© que utilizamos a propriedade `disabled` do HTML para evitar que o bot√£o seja clic√°vel se voc√™ j√° esta na aba correta.

%{
title: "Pattern matching?!",
description: ~H"""
Em Elixir pattern matching √© uma t√©cnica comum e bem poderosa que, quando voc√™ aprende, n√£o consegue ficar sem querer usar. Como o escopo deste curso √© falar sobre LiveView, n√£o sinta que √© necess√°rio que voc√™ pare tudo para estudar mais sobre isso. Como leitura complementar a Elixir School fala sobre isso aqui: <.link navigate="https://elixirschool.com/pt/lessons/basics/functions#pattern-matching-1" target="\_blank">Fun√ß√µes - Pattern Matching</.link>.
"""
} %% .callout

Agora vamos falar do que importa para esta aula: `case`. Assim como o `if` voc√™ precisa come√ßar o condicional com `<%= case (condi√ß√£o aqui) do %>`, √™nfase no `=` pois sem ele nada ser√° renderizado. Como nossa condi√ß√£o passada ao `case` foi `@tab`, cada condi√ß√£o vai essencialmente checar `@tab == 'valor'`. Para cada condi√ß√£o fazemos um `<% 'valor esperado' -> %>` (sem a necessidade de `=`) e finalizamos o bloco com `<% end %>`.

Vale mencionar que no nosso case n√≥s tratamos todas as possibilidades de forma expl√≠cita. E se n√≥s esquecermos uma possibilidade? Crie e execute um arquivo chamado `case_missing.exs`:

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, tab: "home")
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      <%= case @tab do %>
        <% "home" -> %>
          <p>You're on my personal page!</p>
        <% "about" -> %>
          <p>Hi, I'm a LiveView developer!</p>
        <% "contact" -> %>
          <p>Mail me to bot [at] company [dot] com</p>
      <% end %>
    </div>

    <input disabled={@tab == "home"} type="button" value="Open Home" phx-click="show_home" />
    <input disabled={@tab == "about"} type="button" value="Open About" phx-click="show_about" />
    <input disabled={@tab == "contact"} type="button" value="Open Contact" phx-click="show_contact" />
    <input disabled={@tab == "blog"} type="button" value="Open Blog" phx-click="show_blog" />
    """
  end

  def handle_event("show_" <> tab, _params, socket) do
    socket = assign(socket, tab: tab)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

Neste exemplo adicionamos um novo bot√£o para mostrar uma aba de blog por√©m n√£o adicionamos uma cl√°usula no nosso `case` para tratar este valor do nosso assign. Ao clicar em "Open Blog" voc√™ deve notar que sua LiveView reinicia ao estado original e em seu terminal uma exce√ß√£o aparece:

```elixir
07:18:46.498 [error] GenServer #PID<0.376.0> terminating
** (CaseClauseError) no case clause matching: "blog"
    priv/examples/conditional-rendering/case_missing.exs:16: anonymous fn/2 in PageLive.render/1
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/diff.ex:375: Phoenix.LiveView.Diff.traverse/7
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/diff.ex:544: anonymous fn/4 in Phoenix.LiveView.Diff.traverse_dynamic/7
    (elixir 1.16.1) lib/enum.ex:2528: Enum."-reduce/3-lists^foldl/2-0-"/3
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/diff.ex:373: Phoenix.LiveView.Diff.traverse/7
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/diff.ex:139: Phoenix.LiveView.Diff.render/3
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/channel.ex:833: Phoenix.LiveView.Channel.render_diff/3
    (phoenix_live_view 0.18.18) lib/phoenix_live_view/channel.ex:689: Phoenix.LiveView.Channel.handle_changed/4
Last message: %Phoenix.Socket.Message{topic: "lv:phx-F8E02o_S_TzHVAEB", event: "event", payload: %{"event" => "show_blog", "type" => "click", "value" => %{"value" => "Open Blog"}}, ref: "13", join_ref: "12"}
State: %{socket: #Phoenix.LiveView.Socket<id: "phx-F8E02o_S_TzHVAEB", endpoint: LiveviewPlayground.Endpoint, view: PageLive, parent_pid: nil, root_pid: #PID<0.376.0>, router: LiveviewPlayground.Router, assigns: %{tab: "home", __changed__: %{}, flash: %{}, live_action: :index}, transport_pid: #PID<0.371.0>, ...>, components: {%{}, %{}, 1}, topic: "lv:phx-F8E02o_S_TzHVAEB", serializer: Phoenix.Socket.V2.JSONSerializer, join_ref: "12", upload_names: %{}, upload_pids: %{}}
```

A mensagem n√£o poderia ser mais expl√≠cita! Vamos analisar cada peda√ßo:

- A exce√ß√£o √© `CaseClauseError` deixando √≥bvio que falta o tratamento de um caso.
- A pr√≥pria mensagem de erro j√° deixa claro que o caso faltando se chama "blog".
- Se voc√™ descer o olho a "Last message" voc√™ consegue evidenciar que o evento que causou o problema foi o `"show_blog"`. Isto facilita voc√™ a entender que parte da sua LiveView iniciou o problema de modo que voc√™ possa reproduzir localmente e tratar o erro.

Para adicionar uma cl√°usula padr√£o basta usar o formato `<% _ -> %>`. Em Elixir o `_` em contexto de pattern matching significa "qualquer coisa". Poderi√°mos adicionar um conte√∫do padr√£o como `<p>Tab does not exist</p>`.

%{
title: "O que fazer quando n√£o sabemos tratar todos os casos?",
type: :warning,
description: ~H"""
Tudo depende da UX que voc√™ pretende dar ao seu usu√°rio. Usando uma cl√°usula padr√£o voc√™ falha silenciosamente dando ao usu√°rio uma experi√™ncia de que seu sistema est√° incompleto. Se voc√™ intencionalmente deixar sem uma cl√°usula padr√£o o sistema ir√° reiniciar a LiveView o que gera desconforto para seu usu√°rio tamb√©m por√©m se voc√™ tiver um APM voc√™ ver√° a exce√ß√£o e pode corrigi-la em seguida. No futuro iremos discutir valida√ß√µes como solu√ß√£o para estes casos.
"""
} %% .callout

## Cadeias de condi√ß√µes com `cond`

No exemplo anterior usamos `case` para comparar o valor exato da vari√°vel `@tab` em cada cl√°usula. Caso voc√™ tenha a necessidade de renderizar algo baseado em uma condi√ß√£o que n√£o gira em torno de igualdade o `cond` √© perfeito para isso. Crie e execute um arquivo chamado `cond.exs`:

```elixir
Mix.install([
  {:liveview_playground, "~> 0.1.1"}
])

defmodule PageLive do
  use LiveviewPlaygroundWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, temperature_celsius: 30)
    {:ok, socket}
  end

  def render(assigns) do
    ~H"""
    <div>
      Current temperature: <%= @temperature_celsius %>C
    </div>
    <div>
      <%= cond do %>
        <% @temperature_celsius > 40 -> %>
          <p>üî• Impossible to live üî•</p>
        <% @temperature_celsius > 30 -> %>
          <p>Its hot</p>
        <% @temperature_celsius > 20 -> %>
          <p>Kinda cool</p>
        <% @temperature_celsius > 10 -> %>
          <p>Chill</p>
        <% @temperature_celsius > 0 -> %>
          <p>Chill</p>
        <% true -> %>
          <p>‚ùÑÔ∏è‚õÑÔ∏è</p>
      <% end %>
    </div>

    <input type="button" value="Increase" phx-click="increase" />
    <input type="button" value="Decrease" phx-click="decrease" />
    """
  end

  def handle_event("increase", _params, socket) do
    socket = assign(socket, temperature_celsius: socket.assigns.temperature_celsius + 10)
    {:noreply, socket}
  end
  def handle_event("decrease", _params, socket) do
    socket = assign(socket, temperature_celsius: socket.assigns.temperature_celsius - 10)
    {:noreply, socket}
  end
end

LiveviewPlayground.start()
```

Nexte exemplo n√≥s gerenciamos a temperatura em graus Celsius aumentando/diminuindo de 10 em 10. A parte realmente importante do c√≥digo √© justamente nosso condicional. Mais uma vez notamos que apenas a primeira tag possui `=` enquanto as demais n√£o. A primeira diferen√ßa do `cond` para o `case` √© que no `cond` voc√™ sempre come√ßa com `cond do` sem passar nada diferente, as condi√ß√µes s√£o independentes e podem muito bem usar diferentes vari√°vels.

Cada cl√°usula do `cond` segue o formato de predicado (uma express√£o que retorna true ou false) e a primeira condi√ß√£o que passar finaliza o fluxo e renderiza o HTML correspondente. Como a ordem de checagem das cl√°usulas √© de cima para baixo n√£o precisamos fazer checagens como `@temperature_celsius > 30 && @temperature_celsius < 40 ->` pois se a condi√ß√£o `@temperature_celsius > 40 ->` n√£o retornou true j√° sabemos que na segunda cl√°usula j√° estamos com uma temperatura abaixo de 40. Diferente do `case`, para adicionar uma cl√°usula padr√£o n√≥s adicionados `true ->` no final pois como o `true` est√° hardcoded e esta √© a √∫ltima cl√°usula ele sempre vai parar nela.

## Resumindo!

- Para situa√ß√µes de `if-else` voc√™ deve usar explicitamente os blocos `<%= if condi√ß√£o do %>` e `<% else %>`.
- Para situa√ß√µes de apenas `if` voce pode usar o formato de bloco `<%= if condi√ß√£o do %>` ou o atributo HEEx especial `:if={condi√ß√£o}` em uma tag HTML.
- Para m√∫ltiplas compara√ß√µes de um valor voc√™ pode usar o `<%= case valor do %>`.
- Para m√∫ltiplas condi√ß√µes em que n√£o envolvem apenas comparar se um valor √© igual voc√™ pode usar `<%= cond do %>`.
